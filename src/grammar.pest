WHITESPACE = _{ " " | "\t" | "\n" }
separator = _{ "--------" }

gem = _{ SOI ~ item_class 
            ~ rarity 
            ~ name ~ separator
            ~ tags 
            ~ level 
            ~ gem_changes? 
            ~ quality? ~ separator 
            ~ requirements ~ separator 
            ~ description ~ separator
            ~ modifiers? ~ separator?
            ~ experience? ~ separator?
            ~ usage ~ separator?
            ~ corrupted? ~ separator?
            ~ note?}

item_class = _{ "Item Class: " ~ class }
class = { "Support Gems" | "Skill Gems" }

rarity = _{ "Rarity: " ~ rarity_type}
rarity_type = { "Gem" }

name = { ASCII_ALPHA+ }

tags = @{ ((!"\n" ~ ANY)*) }

level = _{ "Level: " ~ gem_level ~ "(Max)"? }
gem_level = { ASCII_DIGIT+ }

gem_changes = @{ (!(separator | quality) ~ ANY)* }

quality = _{ "Quality: " ~ "+" ~ gem_quality ~ "% (augmented)" }
gem_quality = { ASCII_DIGIT+ }

requirements = _{ "Requirements:" ~ (requirement)* }
requirement = { ("Level: " | "Str: " | "Dex: " | "Int: ") ~ ASCII_DIGIT+ }

description = @{ (!separator ~ ANY)* }

modifiers = @{ (!separator ~ ANY)* }

experience = _{ "Experience: " ~ gem_experience }
gem_experience = { ASCII_DIGIT+ ~ "/" ~ ASCII_DIGIT+ }

usage = @{ (!separator ~ ANY)* }

corrupted = { "Corrupted" }

note = { "Note: " ~ ANY+ }